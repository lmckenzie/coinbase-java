buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4.1'
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6"
        classpath 'me.tatarka:gradle-retrolambda:3.6.1'
    }
}

def packageName = 'coinbase-java'
def packageVersion = '2.2.0-108'
def packageDescription = 'Coinbase Android SDK'
group = 'com.coinbase'
version = packageVersion

allprojects {
    repositories {
        jcenter()
    }
    apply plugin: 'com.android.library'
    apply plugin: 'kotlin-android'
    apply plugin: 'com.github.dcendents.android-maven'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'me.tatarka.retrolambda'
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName packageVersion
    }

    lintOptions {
        warning 'InvalidPackage'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), '../proguard-cfg.txt'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    exclude { "com/coinbase/v1/localServerFiles/**" }
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar
    archives javadocJar
}

install {
    repositories.mavenInstaller {
        pom.project {
            name packageName
            description packageDescription
            url 'https://github.com/coinbase/coinbase-java.git'
            inceptionYear '2015'

            packaging 'aar'
            groupId 'com.coinbase'
            artifactId packageName
            version packageVersion

            licenses {
                license {
                    name 'MIT'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection 'https://github.com/coinbase/coinbase-java.git'
                url 'https://github.com/coinbase/coinbase-java'

            }
            developers {
                developer {
                    name 'Coinbase'
                }
            }
        }
    }
}

bintray {
    if (project.hasProperty('bintrayUser')) {
        user = project.property('bintrayUser')
    } else {
        user = ""
    }
    if (project.hasProperty('bintrayKey')) {
        key = project.property('bintrayKey')
    } else {
        key = ""
    }

    configurations = ['archives']
    publish = true
    pkg {
        repo = 'maven'
        name = packageName
        userOrg = 'coinbase'
        desc = packageDescription
        licenses = ['MIT']
        vcsUrl = 'https://github.com/coinbase/coinbase-java.git'
        version {
            name = packageVersion
            desc = 'Coinbase Android SDK'
            released = new Date()
            vcsTag = packageVersion
        }
    }
}


dependencies {
    implementation files('libs/joda-money-0.12.jar')

    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'org.glassfish:javax.annotation:10.0-b28'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'joda-time:joda-time:2.3'
    implementation 'net.sf.opencsv:opencsv:2.3'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-joda:2.4.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.8.8.1'
    implementation 'commons-io:commons-io:2.4'
    implementation 'commons-codec:commons-codec:1.10'
    implementation 'io.reactivex:rxjava:1.3.0'
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'org.apache.commons:commons-lang3:3.5'

    testCompile 'junit:junit:4.12'
    testCompile 'org.powermock:powermock-module-junit4:1.6.4'
    testCompile 'org.powermock:powermock-module-junit4-rule:1.6.2'
    testCompile 'org.powermock:powermock-api-mockito:1.6.4'
    testCompile 'org.powermock:powermock-classloading-xstream:1.6.2'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
repositories {
    mavenCentral()
}
